[  ER.PIN.OFFSET.ADDUPDATE

   Created by
   Ethan Roberts
   on 09/28/2018

   Partial parts of this code was ripped from specfile: "CUTEK.FSCU.OFFSET.ADDUPDATE"
   
   This specfile will find all cards with a Pin Offset change equal to the SYSTEMDATE.

]


TARGET = ACCOUNT

DEFINE
 COUNT = NUMBER
 NEWPINOFFSET = NUMBER
 CARDLOC = NUMBER
 CARDNUM = CHARACTER

 EXPORTFNAME=CHARACTER
 EXPORTFNUMBER=NUMBER
 EXPORTFERROR=CHARACTER
 EXPORTCOUNT = NUMBER

 TEMPCHAR=CHARACTER
 FILLER=""
 CR=CHARACTER(1)
 LF=CHARACTER(1)
 EOL=CHARACTER(2)


END

SETUP
 COUNT = 0
 CR=CTRLCHR(13)
 LF=CTRLCHR(10)
 EOL=CR+LF

 EXPORTFNAME=CHARACTERREAD("Export Letter File Name")

 IF EXPORTFNAME="" THEN
  DO
   PRINT "ERROR: Export Letter File name cannot be blank"
   NEWLINE
   TERMINATE
  END

 FILEDELETE("LETTER",EXPORTFNAME,EXPORTFERROR)

 FILECREATE("LETTER",EXPORTFNAME,EXPORTFERROR)
 IF EXPORTFERROR<>"" THEN
  DO
   PRINT "Error creating Export Letter File "+EXPORTFNAME+": " + EXPORTFERROR
   NEWLINE
   TERMINATE
  END

 FILEOPEN("LETTER",EXPORTFNAME,"APPEND",EXPORTFNUMBER,EXPORTFERROR)
 IF EXPORTFERROR<>"" THEN
  DO
   PRINT "Error opening Export Letter File "+EXPORTFNAME+": " + EXPORTFERROR
   NEWLINE
   TERMINATE
  END

 FILEWRITE(EXPORTFNUMBER,"H",1,EXPORTFERROR)                 [Record ID]
 FILEWRITE(EXPORTFNUMBER,"448849",6,EXPORTFERROR)            [Participant ID]
 FILEWRITE(EXPORTFNUMBER,"7K",2,EXPORTFERROR)                [Processor Value]
 FILEWRITE(EXPORTFNUMBER,"OFF",3,EXPORTFERROR)               [File Type]
 FILEWRITE(EXPORTFNUMBER,"P",1,EXPORTFERROR)                 [Sub Type]
 TEMPCHAR=SEGMENT(FORMAT("9999",FULLYEAR(SYSACTUALDATE)),3,4)+
          FORMAT("99",MONTH(SYSACTUALDATE))+
          FORMAT("99",DAY(SYSACTUALDATE))
 FILEWRITE(EXPORTFNUMBER,TEMPCHAR,6,EXPORTFERROR)            [Creation Date]
 TEMPCHAR=FORMAT("9999",SYSACTUALTIME)+"00"
 FILEWRITE(EXPORTFNUMBER,TEMPCHAR,6,EXPORTFERROR)            [Creation Time]
 FILEWRITE(EXPORTFNUMBER,FILLER,55,EXPORTFERROR)             [Filler - to pad line to 80 characters, not including EOL]
 FILEWRITE(EXPORTFNUMBER,EOL,EXPORTFERROR)

END

SELECT
 ACCOUNT:CLOSEDATE = '--/--/--'
END

PRINT TITLE = "FSCU PIN OFFSET FM"
  

  FOR EACH FMHISTORY WITH FMHISTORY:RECORDTYPE=19 AND                 [affects card record]
                         (FMHISTORY:FMTYPE = 0 OR                     [was a creation]
                          FMHISTORY:FMTYPE = 1) AND                   [was a revision ]
                          FMHISTORY:FIELDNUMBER = 19 AND              [ Pin-Offset field reference ]
                          FMHISTORY:POSTDATE=SYSTEMDATE
   DO
    NEWPINOFFSET = FMHISTORY:NEWNUMBER
    FOR ACCOUNT ACCOUNT:NUMBER
     DO
      FOR EACH CARD WITH CARD:PINOFFSET = NEWPINOFFSET
       DO
        CARDLOC = CARD:LOCATOR
        CARDNUM = CARD:NUMBER
        CALL WRITETODATAFILE
       END
     END
   END
   UNTIL FMHISTORY:POSTDATE < SYSTEMDATE

END



TOTAL
 [Batch Trailer]
 FILEWRITE(EXPORTFNUMBER,"T",1,EXPORTFERROR)                 [Record ID]
 TEMPCHAR=FORMAT(REPEATCHR("9",8),EXPORTCOUNT)
 FILEWRITE(EXPORTFNUMBER,TEMPCHAR,8,EXPORTFERROR)            [Record Count]
 FILEWRITE(EXPORTFNUMBER,FILLER,71,EXPORTFERROR)             [Filler - to pad line to 80 characters, not including EOL]
 FILEWRITE(EXPORTFNUMBER,EOL,EXPORTFERROR)

 FILECLOSE(EXPORTFNUMBER,EXPORTFERROR)
END



PROCEDURE WRITETODATAFILE

   TEMPCHAR="A"
   FILEWRITE(EXPORTFNUMBER,TEMPCHAR,1,EXPORTFERROR)                            [Action]
   FILEWRITE(EXPORTFNUMBER,CARDNUM,19,EXPORTFERROR)                            [Customer ID Number]
   FILEWRITE(EXPORTFNUMBER,"0000",4,EXPORTFERROR)                              [Member Number]
   FILEWRITE(EXPORTFNUMBER,FORMAT("9999",NEWPINOFFSET),4,EXPORTFERROR)         [PIN Offset]
   FILEWRITE(EXPORTFNUMBER,FILLER,52,EXPORTFERROR)                             [Filler]  
   FILEWRITE(EXPORTFNUMBER,EOL,EXPORTFERROR)
   EXPORTCOUNT = EXPORTCOUNT+1
 
END

